# User account
# This file contains an example FSM for a user account featuring nested compound states

#
# Description of all the states
#

STATE initial _ "" : INITIAL;
STATE created _ "Created" : COMPOUND;
STATE inactive created "Inactive" : COMPOUND;
STATE active created "Active" : COMPOUND;
STATE unverified inactive "Unverified" : SIMPLE;
STATE blocked inactive "Blocked" : SIMPLE;
STATE deleted inactive "Deleted" : SIMPLE;
STATE verified active "Verified" : SIMPLE;
STATE loggedIn active "Logged in" : SIMPLE;
STATE final _ "Archived" : FINAL;

#
# Description of all the triggers
#

TRIGGER create "create";
TRIGGER timerElapsed "timer elapsed";
TRIGGER emailVerification "email verification";
TRIGGER blockedByAdmin "blocked by admin";
TRIGGER unblockedByAdmin "unblocked by admin";
TRIGGER deletedByAdmin "deleted by admin";
TRIGGER forgetMe "forget me";
TRIGGER login "login";
TRIGGER logout "logout";
TRIGGER archive "archive";

#
# Description of all the actions
#

ACTION unverified "send confirmation mail" : ENTRY_ACTION;
ACTION unverified "start timer" : ENTRY_ACTION;
ACTION unverified "stop timer" : EXIT_ACTION;
ACTION blocked "notify user" : ENTRY_ACTION;
ACTION active "logout" : EXIT_ACTION;
ACTION verified "notify user" : ENTRY_ACTION;
ACTION deleted "anonymize" : ENTRY_ACTION;
ACTION loggedIn "reset attempts" : ENTRY_ACTION;
ACTION loggedIn "log activity" : DO_ACTION;

ACTION unverifiedToVerified "attempts = 0" : TRANSITION_ACTION;
ACTION verifiedToVerified "attempts++" : TRANSITION_ACTION;

#
# Description of all the transitions
#

TRANSITION initialToUnverified initial -> unverified create "";
TRANSITION unverifiedToVerified unverified -> verified emailVerification "";
TRANSITION verifiedToVerified verified -> verified login "invalid credentials";
TRANSITION verifiedToLoggedIn verified -> loggedIn login "valid credentials";
TRANSITION loggedInToVerified loggedIn -> verified logout "";
TRANSITION loggedInToDeleted loggedIn -> deleted forgetMe "valid credentials";
TRANSITION verifiedToBlocked verified -> blocked "attempts >= 3";
TRANSITION activeToBlocked active -> blocked blockedByAdmin "";
TRANSITION blockedToVerified blocked -> verified unblockedByAdmin "";
TRANSITION blockedToDeleted blocked -> deleted deletedByAdmin "";
TRANSITION deletedToFinal deleted -> final archive "";
TRANSITION unverifiedToDeleted unverified -> deleted timerElapsed "";
